buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java'

version = '1.16.5-6.3.0.0'
group = 'yamahari.ilikewood'
archivesBaseName = 'ilikewood'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8 // Need this here so eclipse task generates correctly.

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources', 'src/data/resources']
        }
    }

    api {
        java {
            srcDirs = ['src/api/java']
        }
        resources {
            srcDirs = ['src/api/resources']
        }
    }
}

configurations {
//apiImplementation.extendsFrom(implementation)
//    apiCompile.extendsFrom(compile)
//    apiCompileOnly.extendsFrom(compileOnly)
//    apiRuntimeOnly.extendsFrom(runtimeOnly)
    apiImplementation.extendsFrom(minecraft)
}

minecraft {
    mappings channel: 'official', version: '1.16.5'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'ilikewood.datagen.modid', 'ilikewood'
            args '--mod', 'ilikewood',
                    '--all',
                    '--output', file('src/data/resources/'),
                    '--existing', file('src/main/resources/')
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }


    }
}

repositories {
    flatDir {
        dirs 'build/libs'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.0'

    compileOnly fg.deobf('mezz.jei:jei-1.16.5:7.+:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.5:7.+')

    //runtimeOnly fg.deobf('build.libs:cofh_core:1.16.3-1.1.6')
    //runtimeOnly fg.deobf('build.libs:thermal_foundation:1.16.3-1.1.6')
    //runtimeOnly fg.deobf('build.libs:thermal_expansion:1.16.3-1.1.6')

    // minecolonies-0.14.73-ALPHA-universal
    //runtimeOnly fg.deobf('build.libs:minecolonies:0.14.73-ALPHA:universal')
    // structurize-0.13.148-ALPHA-universal
    //runtimeOnly fg.deobf('build.libs:structurize:0.13.148-ALPHA:universal')
    // ilikewoodxbiomesoplenty-1.16.5-6.2.2.0
    //runtimeOnly fg.deobf('build.libs:ilikewoodxbiomesoplenty:1.16.5-6.2.2.0')
    // BiomesOPlenty-1.16.4-13.0.0.432-universal
    //runtimeOnly fg.deobf('build.libs:BiomesOPlenty:1.16.4-13.0.0.432:universal')

    implementation sourceSets.api.output
}

jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.api.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir

    sourceSets.main.resources {
        exclude 'assets/biomesoplenty/**'
        exclude '.cache'
    }

    manifest.attributes([
                "Specification-Title"     : "ilikewood",
                "Specification-Vendor"    : "yamahari",
                "Specification-Version"   : "${project.version}",
                "Implementation-Title"    : "ilikewood",
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "yamahari",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])

    finalizedBy 'reobfJar'
}

task apiJar(type: Jar, dependsOn: apiClasses) {
    archiveClassifier.set('api')
    from sourceSets.api.output
    afterEvaluate { finalizedBy reobfApiJar }
}

reobf {
    apiJar { classpath.from(sourceSets.api.compileClasspath) }
    jar { classpath.from(sourceSets.main.compileClasspath) }
}

task reobf {
    dependsOn reobfJar
    dependsOn reobfApiJar
}