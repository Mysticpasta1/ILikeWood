buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java'

version = '1.16.3-4.0.2.2'
group = 'yamahari.ilikewood'
archivesBaseName = 'ilikewood'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8 // Need this here so eclipse task generates correctly.

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }

    api {
        java {
            srcDirs = ['src/api/java']
        }
        resources {
            srcDirs = ['src/api/resources']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

configurations {
    apiCompile.extendsFrom(compile)
}

minecraft {
    mappings channel: 'snapshot', version: '20200916-1.16.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'ilikewood.datagen.modid', 'ilikewood'
            args '--mod', 'ilikewood', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        dataBoP {
            parent data
            property 'ilikewood.datagen.modid', 'biomesoplenty'
        }
    }
}

repositories {
    flatDir {
        dirs 'build/libs'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.3-34.1.0'

    compileOnly fg.deobf('com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.16.3-12.0.0.400:api')
    runtimeOnly fg.deobf('build.libs:BiomesOPlenty:1.16.3-12.0.0.400:universal')

    compileOnly fg.deobf('mezz.jei:jei-1.16.3:7.+:api')
    runtimeOnly fg.deobf('mezz.jei:jei-1.16.3:7.+')

    implementation sourceSets.api.output
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.api.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir
    sourceSets.main.resources {
        exclude 'assets/biomesoplenty/**'
    }
    manifest {
        attributes([
                "Specification-Title"     : "ilikewood",
                "Specification-Vendor"    : "yamahari",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "ilikewood",
                "Implementation-Version"  : "1.16.3-4.0.2.2",
                "Implementation-Vendor"   : "yamahari",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}